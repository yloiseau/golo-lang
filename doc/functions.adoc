== Functions

Functions are first-class citizen in Golo. Here is how to define and
call some.

=== Parameter-less functions

Golo modules can define functions as follows:

[source,golo]
----
module sample

function hello = {
  return "Hello!"
}
----

In turn, you may invoke a function with a familiar notation:

[source,golo]
----
let str = hello()
----

A function needs to return a value using the `return` keyword. Some
languages state that the last statement is the return value, but Golo
does not follow that trend. We believe that `return` is more explicit,
and that a few keystrokes in favour of readability is still a good deal.

Still, you may omit `return` statements if your function does not return
a value:

[source,golo]
----
function printer = {
  println("Hey!")
}
----

If you do so, the function will actually return `null`, hence `result`
in the next statement is `null`:

[source,golo]
----
# result will be null
let result = printer()
----

=== Functions with parameters

Of course functions may take some parameters, as in:

[source,golo]
----
function addition = |a, b| {
  return a + b
}
----

NOTE: Parameters are constant references, hence they cannot be reassigned.

Invoking functions that take parameters is straightforward, too:

[source,golo]
----
let three = addition(1, 2)
let hello_world = addition("hello ", "world!")
----

=== Variable-arity functions

Functions may take a varying number of parameters. To define one, just
add `...` to the last parameter name:

[source,golo]
----
function foo = |a, b, c...| {
  # ...
}
----

Here, `c` catches the variable arguments in an array, just like it would
be the case with Java. You can thus treat `c` as being a Java object of
type `Object[]`.

Calling variable-arity functions does not require wrapping the last
arguments in an array. While invoking the `foo` function above, the
following examples are legit:

[source,golo]
----
# a=1, b=2, c=[]
foo(1, 2)

# a=1, b=2, c=[3]
foo(1, 2, 3)

# a=1, b=2, c=[3,4]
foo(1, 2, 3, 4)
----

Because the parameter that catches the last arguments is an array, you
may call array methods. Given:

[source,golo]
----
function elementAt = |index, args...| {
  return args: get(index)
}
----

then:

[source,golo]
----
# prints "2"
println(elementAt(1, 1, 2, 3))
----

=== Named parameters

When you invoke Golo functions, you can use the name of the parameters explicitly in the call like so:

[source,golo]
----
function post = |title, body, promoted, tags...| {
  let data = map[
    ["title", title],
    ["body", body],
    ["promoted", promoted],
    ["tags", tags: asList()]
  ]
  return gololang.JSON.stringify(data)
}

post(
  tags = array["feature", "syntax"],
  body = "it rocks!"
  title = "Named parameters are alive",
  promoted = true
)
----

NOTE: Once you are using named parameters in your function call, the order doesn't matter anymore.

NOTE: To name varargs argument, you have be box the values into an `array[]` (just has it's done with the `tags` argument in the above snippet)

CAUTION: You must name either or none of the arguments. A compilation error will be raised, if you mix named an unamed arguments in a function invocation.

=== Functions from other modules and imports

Suppose that we have a module `foo.Bar`:

[source,golo]
----
module foo.Bar

function f = {
  return "f()"
}
----

We can invoke `f` from another module by prefixing it with its module
name:

[source,golo]
----
let r = foo.Bar.f()
----

Of course, we may also take advantage of an `import` statement:

[source,golo]
----
module Somewhere.Else

import foo.Bar

function plop = {
  return f()
}
----

NOTE: Imports in Golo do not work as in Java.
Golo is a dynamic language where symbols are being resolved at runtime. Module imports are
**not** checked at compilation time, and their sole purpose is to help in dynamic resolution. Back
to the previous example, `f` cannot be resolved from the current module, and the Golo runtime
subsequently tries to resolve `f` from each `import` statement. Also, note that the order of
`import` statements is important, as the resolution stops at the first module having the `f`
function.

You may prepend the *last* piece of the module name. The following invocations are
equivalent:

[source,golo]
----
module Somewhere.Else

import foo.Bar

function plop = {
  let result = f()
  let result_bis = Bar.f()
  let result_full = foo.Bar.f()
  return result
}
----

[[relative_import]]
To help maintaining packages of several modules, and to avoid repeating the fully qualified name of the package when importing “local” modules,
`import` can also be made relative to the package of the importing module. For instance, the following code:

[source,golo]
----
module foo.bar.Spam

import .baz.Egg
----

is equivalent to

[source,golo]
----
module foo.bar.Spam

import foo.bar.baz.Egg
----

Note that only modules in the same package or in a sub-package can be imported using relative name. In the previous example, to import the module `foo.Plop`, its full name must be specified.

[[multi_import]]
Moreover, it is possible to import several modules from the same package with a single `import` statement, as in;

[source,golo]
----
import java.util.{Collections, Objects, stream.Collectors}
----

[[implicit_imports]]
Golo modules have a set of implicit imports:

* `gololang.Predefined`,
* `gololang.StandardAugmentations`,
* `gololang`,
* `java.lang`.

These modules are imported *after* the module explicitly imported in the module, so that elements defined in these modules (e.g. predefined functions or <<augment_order,augmentations>>) can be redefined.

[[warning-unavailable-class]]
[WARNING]
====
When an imported module can't be loaded, either because it is not in the classpath or because of a typo in its name, a warning can printed by setting the `golo.warnings.unavailable-class` system property to `true`.
This does not preclude the code to work correctly since the looked up function can be present in another imported module.
When the function is called prefixed with the module name, and the module can't be loaded, the warning is also printed, but the code will fail with a `NoSuchMethodError`.
This option is mainly for debugging purpose and is not activated by default. Indeed, due to the way Golo load modules and lookup functions, many “false positives” can be reported. This can be changed in a future release.
====


=== Local functions

By default, functions are visible outside of their module. You may
restrict the visibility of a function by using the `local` keyword:

[source,golo]
----
module Foo

local function a = {
  return 666
}

function b = {
  return a()
}
----

Here, `b` is visible while `a` can only be invoked from within the `Foo`
module. Given another module called `Bogus`, the following would fail at
runtime:

[source,golo]
----
module Bogus

function i_will_crash = {
  return Foo.a()
}
----


=== Recursive Tail Call Optimization

Golo feature recursive https://en.wikipedia.org/wiki/Tail_call[tail call] optimization. If a function last action is to return the result of a recursive call, it is optimized to not stack a new call and is compiled in code equivalent to a loop. For instance, a function like:

[source,golo]
----
function fact = |acc, v| -> match {
  when v == 0 then acc
  otherwise fact(acc * v, v - 1)
}
----

is compiled into something roughly equivalent to :

[source,golo]
----
function fact = |acc, v| {
  var acc_ = acc
  var v_ = v
  while v_ != 0 {
    acc_ = acc_ * v_
    v_ = v_ - 1
  }
  return acc_
}
----

This allows to create recursive functions that will not throw a `StackOverflowError` even in the presence of a large number or recursive call.
The optimization can be disabled by setting the `golo.optimize.tce` system property to `false` (e.g. `export GOLO_OPTS='-Dgolo.optimize.tce=false`).

A call is tail recursive when the function returns the result of calling itself directly. Indeed, since no evaluation nor flow analysis is done, many effectively tail recursive calls can't be identified as such, and thus are not optimized. It is recommended to rewrite the code to make the tail call more direct. For instance, the following two functions are not optimized:

[source,golo]
----
function foo = |a, v| {
  let r = ^foo
  if v == 0 {
    return a
  }
  return r(a + v, v - 1)
}

function bar = |a, v| {
  var r = null
  if v == 0 {
    r = a
  } else {
    r = bar(a + v, v - 1)
  }
  return r
}
----

while the following one is:
[source,golo]
----
function ref = |a, v| {
  if v == 0 {
    return a
  }
  return ref(a + v, v - 1)
}
----

Note that returning a `match` expression that may evaluate to a tail call will be optimized, such that the function
[source,golo]
----
function baz = |a, v| -> match {
  when v == 0 then a
  otherwise ref(a + v, v - 1)
}
----
will be strictly equivalent to the previous one. A consequence of this behavior is that mutual recursions are not optimized.

==== On augmentations

Functions defined in augmentations can be optimized if written in a tail call style. For instance, in the following sample, the `reduce` function is not optimized since it is viewed as a method call:
[source,golo]
----
struct Cons = {head, tail}

augment Cons {
  function reduce = |this, f, z| -> match {
    when this: isEmpty() then z
    otherwise this: tail(): reduce(f, f(this: head(), z))
  }
}
----

On the other hand, the following one is optimized and works as expected:
[source,golo]
----
augment Cons {
  function reduce = |this, f, z| -> match {
    when this: isEmpty() then z
    otherwise reduce(this: tail(), f, f(this: head(), z))
  }
}
----

==== Limitations on decorators

Since the optimization is done at compile time, most of dynamic features are not available. For instance, <<decorators,decorated functions>>
can't be optimized. Indeed, in that case, the decorator would not be applied on each call but only on the first one, which could lead to unexpected results.

For instance, the code:

[source,golo]
----
function log = |f| -> |a, v| {
  println("function called with " + a + ", " + v)
  return f(a, v)
}

@log
function fact = |acc, v| -> match {
  when v == 0 then acc
  otherwise fact(acc * v, v - 1)
}
----

when optimised would print the message only for the first call. The tail call optimization is thus disabled for decorated functions. If the desired behavior is to optimize the function and apply the decorator only for the first call, one can create an undecorated version, and decorate explicitly direct calls or create a decorated wrapper, as in:

[source,golo]
----
function log = |f| -> |a, v| {
  println("function called with " + a + ", " + v)
  return f(a, v)
}

function fact = |acc, v| -> match {
  when v == 0 then acc
  otherwise fact(acc * v, v - 1)
}

@log
function decoratedFact = |acc, v| -> fact(acc, v)

#(...)

let direclyDecorated = log(^fact)
directlyDecorated(1, 5)
log(^fact)(1, 5)
----

Note that this approach is not as cumbersome, since most of the time, tail recursive functions introduce an “artificial” accumulator that is hidden by a function calling the recursive one with the default accumulator value.
For instance, in the factorial case, one would write:
[source,golo]
----
local function fact = |acc, v| -> match {
  when v == 0 then acc
  otherwise fact(acc * v, v - 1)
}

function fact = |v| -> fact(1, v)
----

or equivalently:
[source,golo]
----
function fact = |n| {
  let _fact = |acc, v| -> match {
    when v == 0 then acc
    otherwise _fact(acc * v, v - 1)
  }
  return _fact(1, n)
}
----

In this case, the both the local `fact` and the `_fact` closure are optimized, while the public one can be decorated.


Similarly, variadic functions are not optimized, since we can't know at compile-time if the last argument is already an array or a simple value that must be collected. The same kind of approach is recommended, by defining a (local) fixed arguments recursive version that is optimized and a variadic one that delegates on it.



=== Module-level state

You can declare `let` and `var` references at the module level, as in:

[source,golo]
----
module Sample

let a = 1

var b = truth()

local function truth = {
  return 42
}
----

These references get initialized when the module is being loaded by the Java virtual machine. In
fact, module-level state is implemented using `private static` fields that get initialized in a
`<clinit>` method.

Module-level references are only visible from their module, although a function may provide
accessors to them.

It is important to note that such references get initialized in the order of declaration in the
source file. Having initialization dependencies between such references would be silly anyway, but
one should keep it in mind _just in case_.

CAUTION: Global state is a bad thing in general. We strongly advise you to *think twice* before you
introduce module-level state. Beware of potential memory leaks, just like `static` class fields in
the Java programming language.
